rule APT_Loader_Win32_REDFLARE_1
{
	meta:
		date_created = "2020-11-27"
		date_modified = "2020-11-27"
		md5 = "01d68343ac46db6065f888a094edfe4f"
		rev = 1
		author = "FireEye"
		description = "Detects the presence of APT Loader Win32 REDFLARE variant 1"
		os = "windows"
		filetype = "executable"

	strings:
		$alloc_n_load = { 6A 40 68 00 30 00 00 [0-20] 6A 00 [0-20] FF D0 [4-60] F3 A4 [30-100] 6B C0 28 8B 4D ?? 8B 4C 01 10 8B 55 ?? 6B D2 28 }
		$const_values = { 0F B6 ?? 83 C? 20 83 F? 6D [2-20] 83 C? 20 83 F? 7A }

	condition:
		( uint16(0)==0x5A4D) and ( uint32( uint32(0x3C))==0x00004550) and ( uint16( uint32(0x3C)+0x18)==0x010B) and all of them
}
