rule APT_HackTool_MSIL_SHARPSTOMP_1
{
	meta:
		date_created = "2020-12-02"
		date_modified = "2020-12-02"
		md5 = "83ed748cd94576700268d35666bf3e01"
		rev = 3
		author = "FireEye"
		description = "Detects the presence of APT_HackTool_MSIL_SHARPSTOMP_1"
		os = "windows"
		filetype = "executable"

	strings:
		$s0 = "mscoree.dll" fullword nocase
		$s1 = "timestompfile" fullword nocase
		$s2 = "sharpstomp" fullword nocase
		$s3 = "GetLastWriteTime" fullword
		$s4 = "SetLastWriteTime" fullword
		$s5 = "GetCreationTime" fullword
		$s6 = "SetCreationTime" fullword
		$s7 = "GetLastAccessTime" fullword
		$s8 = "SetLastAccessTime" fullword

	condition:
		( uint16(0)==0x5A4D) and ( uint32( uint32(0x3C))==0x00004550) and all of them
}
