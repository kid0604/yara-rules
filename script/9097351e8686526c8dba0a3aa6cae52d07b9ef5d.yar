import "pe"

rule APT_Builder_PY_REDFLARE_1
{
	meta:
		date_created = "2020-11-27"
		date_modified = "2020-11-27"
		md5 = "d0a830403e56ebaa4bfbe87dbfdee44f"
		rev = 1
		author = "FireEye"
		description = "Yara rule for detecting APT_Builder_PY_REDFLARE_1 malware"
		os = "windows"
		filetype = "script"

	strings:
		$1 = "LOAD_OFFSET_32 = 0x612"
		$2 = "LOAD_OFFSET_64 = 0x611"
		$3 = "class RC4:"
		$4 = "struct.pack('<Q' if is64b else '<L'"
		$5 = "stagerConfig['comms']['config']"
		$6 = "_x86.dll"
		$7 = "_x64.dll"

	condition:
		all of them and @1[1]<@2[1] and @2[1]<@3[1] and @3[1]<@4[1] and @4[1]<@5[1]
}
